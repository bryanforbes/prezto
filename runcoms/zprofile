#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vi'
export VISUAL='vi'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU path cdpath fpath manpath mailpath

path=(
	/usr/local/{bin,sbin}
	$path
)

if [[ -d "$HOME/.cargo/bin" ]]; then
	path=(
		~/.cargo/bin
		$path
	)
fi

# Initialize manpath with the default system paths
MANPATH=$(manpath -q)

case "$(uname)" in
	Darwin)
		# path and manpath for homebrew
		path=(
			/usr/local/opt/{coreutils,gnu-sed}/libexec/gnubin
			$path
		)
		manpath=(
			/usr/local/opt/{coreutils,gnu-sed}/libexec/gnuman
			$manpath
		)
		;;
esac

if [ -e "$HOME/Projects" ]; then
	if [ -e "$HOME/Projects/sitepen" ]; then
		cdpath=(
			~/Projects/sitepen
			$cdpath
		)
	fi
	cdpath=(~/Projects $cdpath)
	if [ -e "$HOME/Projects/pebble/PebbleSDK-current" ]; then
		path=(
			~/Projects/pebble/PebbleSDK-current/bin
			$path
		)
	fi
fi

# Set the list of directories that cd searches.
cdpath=(
	.
	~
	$cdpath
)

# Set the list of directories that Zsh searches for programs.
path=(
	~/{bin,.dotfiles/bin}
	$path
)

fpath=(
	~/.zprezto/functions
	$fpath
)
autoload -Uz ~/.zprezto/functions/*(:t)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

export PIPENV_VENV_IN_PROJECT=1
